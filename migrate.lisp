;; CL-MIGRATIONS is released under the MIT Licence: 
;;
;; Copyright (c) <2006> <Vamsee Krishna Kanakala>
;;
;; Permission is hereby granted, free of charge, to any person 
;; obtaining a copy of this software and associated documentation 
;; files (the "Software"), to deal in the Software without restriction, 
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to permit persons to whom the Software is furnished to do so, 
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be
;; included in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, 
;; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
;; OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND 
;; NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT 
;; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, 
;; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, 
;; ARISING FROM, OUT OF OR IN CONNECTION WITH THE
;; SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

(in-package :cl-migrations)

(defun get-db-version ()
  (parse-integer
   (first (select 'version
            :from *schema-table-name*
            :flatp t
            :field-names nil))))

#.(locally-enable-sql-reader-syntax)
(defun incf-db-version (db-version mig-version)
  (let ((db-version/string (princ-to-string db-version))
        (mig-version/string (princ-to-string mig-version)))
    (update-records (make-symbol *schema-table-name*)
                    :av-pairs `((version ,mig-version/string))
                    :where [= [version] db-version/string])))

(defun decf-db-version (db-version mig-version)
  (let ((db-version/string (princ-to-string db-version))
        (mig-version/string (princ-to-string mig-version)))
    (update-records (make-symbol *schema-table-name*)
                    :av-pairs `((version ,mig-version/string))
                    :where [= [version] db-version/string])))
#.(restore-sql-reader-syntax-state)

(defun get-migration-number (file)
  (parse-integer 
   (subseq (file-namestring file) 0 (search "-" (file-namestring file))) :junk-allowed t))

(defun compare-files (file1 file2)
   (< (get-migration-number file1) (get-migration-number file2)))

(defun get-migration-files ()
  "Get all files from the migrations directory, and sort them numerically."
  (unless *migration-dir*
    (warn "Migrations directory not set properly. Please check your config file.")
    (return-from get-migration-files :skipped))
  (let ((files (directory (make-pathname :name :wild
					 :type "lisp"
					 :defaults *migration-dir*))))
    (sort (remove-if (lambda(x) (null (get-migration-number x))) files)
	  #'compare-files)))

(defun get-latest-migration ()
  "Get the version of latest migration available."
  (let ((files-list (get-migration-files)))
    (unless (eql files-list :skipped)
      (get-migration-number (first (last files-list))))))

(defun current-date-string ()
  "Generates the current date as string of format YYYYMMDDhhmmss."
  (let ((date (reverse (subseq (multiple-value-list (get-decoded-time)) 0 6))))
    (format nil "铂О宁溽翦┅ㄤ彐躅珏铄蜥翦钺礤⑶孱弪狒犷屙痿黹珧狒轱骈戾鏖翳犷狍箝珙邃鲥蝮轱铛礅弪躅戾篌黹珧狒轱瞽溟颡蝈徜箴邈螬戾舄è骈戾钺礤磲脲疳翳钺礤侯犴ㄣ镱汜翦钺翦篝蜷铉ㄣ躜蝈铘溽翦篝蜷铉钺礤呼疱㈧轶稷轰彐狨祠黹珧狒轱瞽溟颡┅ㄦ殪瀛钺礤篝ㄦ殪瀛钺礤篝蜷铉骈戾钺礤┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶骈戾钺礤鏖翳镳孱骈戾篝蝈犴骈戾钺礤轰轵邈糸镱猴豸瘐洪姝滹弩铒舡屮轶恒蝈狒濠黩轸瀛扉铄ㄣ镱汜翦钺翦篝蜷铉⒒骈戾钺礤篝颟篝蝈犴鏖翳篝犷溽蜾轱簌铘狲痱轭Ж乎ī轰秣ī篝蝈犴┅骈戾钺礤篝颟ㄤ彐躅珏舡骈戾蜥铉ㄦ蝻麸⑶弭翳黹珧狒轱骈戾鏖翳轭翳玳鲥蜥铉瀣轭狍沐钿轭矧溴虍蝈盹鲥殒＇灬礅溽ㄦ殪濠矧ㄧ弭黹珧狒轱瞽铛礅弪骈戾骝镯ㄧ弭黹珧狒轱瞽铛礅弪骈戾麸┅ㄧ弭黹珧狒轱瞽骈戾螬┅ㄤ彐躅箦戾泗黹珧狒轱瞽骈戾ㄤ猸鲥蝮轱黹绛鲥蝮轱瞟⒁弭躜黹珧狒轱骈戾轭狍沐钿轭矧溴筱孱溟铉矧溴忉箦镱麒弭桢黹珧狒轭躔矧滹黝蝈箴邈糸鲥禊ㄩ黹绛鲥蝮轱溻鲥蝮轱瞟ㄧ弭骈戾蜥铉ū溻鲥蝮轱瞟黹绛鲥蝮轱瞟蝈鲥蝮ㄧ弭骈戾蜥铉ū黹绛鲥蝮轱瞟溻鲥蝮轱瞟┅ㄤ彐躅屮邈黹珧狒轱铙ㄤ猸鲥蝮轱黹绛鲥蝮轱瞟⑴沲翦犰翳黹珧狒轱铙鏖翳轭翳玳鲥蜥铉轭趄犷筢泗轱町戾è骈戾箦戾泗黹珧狒轱瞽骈戾溻鲥蝮轱黹绛鲥蝮轱瞟┅鏖翳趄犷筢泗轱īㄤ镬轶ㄦ殪骈戾戾铉翳骈戾螬ㄦ矧磲ネ殓蜥糸镱￣雍ㄧ弭黹珧狒轱瞽铛礅弪骈戾┅鏖翳镳孱骈戾篝蝈犴骈戾鏖翳篝犷溽蜾轱簌铘狲戾舄è黹珧狒轱蝈徜篝蝈犴┅ㄤ潇ㄩ黹绛鲥蝮轱溻鲥蝮轱瞟ㄧ弭黹珧狒轱乎皓ㄧ弭黹珧狒轱轰秣瞟┅ㄤ镬轶篝狒屙孱滗飑ㄦ矧磲ヅ嘏煤英ㄦ轵篝篝狒屙孱舂ㄥ邈豸瀛泔眄犷ㄦ轵篝篝狒屙孱舂┅┅ㄩ黹绛鲥蝮轱溻鲥蝮轱瞟ㄩ钽姝溻鲥蝮轱溻鲥蝮轱黹绛鲥蝮轱瞟ㄤ邈姝溻鲥蝮轱溻鲥蝮轱黹绛鲥蝮轱瞟┅┅ㄤ彐躅黹珧狒é脲鲥蝮轱瞟⑸铋糸狒黹珧狒轱痱镢邃躜瀹麒孱ㄩ铋舡泔铈殓躅鏖钿痱雉邈戾è溻鲥蝮轱ㄧ弭溻鲥蝮轱瞟黹绛鲥蝮轱ㄧ弭灬翦篝黹珧狒轱瞟┅躅戾篌黹绛鲥蝮轱蝈趱蝾骝镯黹珧狒后腴痧邃┅躅戾篌鲥蝮轱箦翩鲥蝮轱黹绛鲥蝮轱瞟ㄣ镱è鲥蝮轱黹绛鲥蝮轱瞟麽蝾⑼殓蜥糸镱铒痱弩孱舢蝈趱蝾骝镯黹珧狒后腴痧邃┅è羼踽溻鲥蝮轱鲥蝮轱瞟麽蝾⑽雉栝铉麸黹珧狒瀹┅ㄦ矧磲キピ雉犰黹珧狒轱铙英ㄥ邈黹珧狒轱铙溻鲥蝮轱鲥蝮轱瞟┅┅ㄤ轶泔铑邈舂┅